<program>       := <func>*
<func>          := <ident> ("(" (<ident> ("," <ident>)*)? ")")? <stmt>
<stmt>          := <expr> ";" 
                    | "{" <stmt>* "}"
                    | "if" "(" <expr> ")" <stmt> ("else" <stmt>)?
                    | "while" "(" <expr> ")" <stmt>
                    | "for" "(" <expr>? ";" <expr>? ";" <expr>? ")" <stmt>
                    | "return" <expr> ";"
<expr>          := <assign>
<assign>        := <equality> ("=" <assign>)?
<equality>      := <relational> ("==" <relational> | "!=" <relational>)*
<relational>    := <add> ("<" <add> | "<=" <add> | ">" <add> | ">=" <add>)*
<add>           := <mul> ("+" <mul> | "-" <mul>)* 
<mul>           := <unary> ("*" <unary> | "/" <unary> | "%" <unary>)* 
<unary>         := "+"? <primary> 
                    | "-"? <primary>
                    | "*" <unary>
                    | "&" <unary>
<primary>       := <num> 
                    | <ident> ("(" (<expr> ("," <expr>)*)? ")")?
                    | "(" <expr> ")"