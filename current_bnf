<program>                       := <func>*
<func>                          := "int" "*"* <ident>  ("(" ("int" "*"* <ident> ("," "int" "*"* <ident>)*)? ")")? <stmt>
<stmt>                          := <expr> ";" 
                                    | "{" <stmt>* "}"
                                    | "if" "(" <expr> ")" <stmt> ("else" <stmt>)?
                                    | "while" "(" <expr> ")" <stmt>
                                    | "for" "(" <expr>? ";" <expr>? ";" <expr>? ")" <stmt>
                                    | "return" <expr> ";"
                                    | <declaration> ";"

<declaration>                   := <declaration-specifiers> <init-declarator>*
<declaration-specifiers>        := <type-specifiers>
<type-specifiers>               := "int"

<init-declarator>               := <declarator>
<declarator>                    := <pointer>? <direct-declarator>
<direct-declarator>             := <identifier>
                                    | <identifier> "[" <assign> "]"
<pointer>                       := "*" <pointer>

<expr>                          := <assign>
<assign>                        := <equality> ("=" <assign>)?
<equality>                      := <relational> ("==" <relational> | "!=" <relational>)*
<relational>                    := <add> ("<" <add> | "<=" <add> | ">" <add> | ">=" <add>)*
<add>                           := <mul> ("+" <mul> | "-" <mul>)* 
<mul>                           := <unary> ("*" <unary> | "/" <unary> | "%" <unary>)* 
<unary>                         := <postfix> 
                                    |"sizeof" <unary>
                                    | "+" <unary> 
                                    | "-" <unary>
                                    | "*" <unary>
                                    | "&" <unary>
<postfix>                       := <primary>
                                    | <primary> "[" <expr> "]"
<primary>                       := <num> 
                                    | <ident> ("(" (<expr> ("," <expr>)*)? ")")?
                                    | "(" <expr> ")"
